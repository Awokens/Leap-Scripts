at 21:00 in world "world":

	add 1 to {wardenTime}

	if {wardenTime} < 14:
		exit
	
	kill wardens
	set {wardenTime} to 0
	set gamerule announceAdvancements of world "world" to false
	set gamerule announceAdvancements of world "world_nether" to false
	set gamerule announceAdvancements of world "world_the_end" to false
	execute console command "stopsound @a"
	apply darkness to players for 20 seconds
	play sound "minecraft:music.overworld.deep_dark" at volume 0.5 to players
	wait 5 seconds

	set {_s::*} to "&ffortnight's eve&7, the &f&kwarden&r&7 guards", "&7the &fgate&7 where mystery spawns." and "&fDon &7the &fthird eye&7, ascend to the sky"
	loop {_s::*}:
		send title "" with subtitle loop-value to players for 1 second with fadein 1 second and fade out 1 second
		play sound "minecraft:entity.warden.heartbeat" at volume 0.5 to players
		play sound "minecraft:ui.cartography_table.take_result" at volume 0.2 to players
		wait 4 seconds
		
	spawn warden at block above random element out of players
	set {_warden} to last spawned warden
	set {_lifespan} to now
	set max health of {_warden} to 1500
	set health of {_warden} to 1500
	set gamerule announceAdvancements of world "world" to true
	set gamerule announceAdvancements of world "world_nether" to true
	set gamerule announceAdvancements of world "world_the_end" to true
	set metadata tag "lastAttack" of {_warden} to now

	while {_warden} is alive:
		wait 1 second
		if difference between {_lifespan} and now > 15 minutes:
			kill wardens
			exit loop
		if block below {_warden} is bedrock:
			teleport {_warden} to block above (random element out of players)
			play sound "entity.enderman.teleport" at {_warden}
		set {_last} to metadata tag "lastAttack" of {_warden} # initialize
		difference between {_last} and now >= 10 seconds # condition

		teleport {_warden} to block above (random element out of players)
		play sound "entity.enderman.teleport" at {_warden}

on target:
	if event-entity is a mob:
		if block below target is bedrock:
			cancel event

on damage of warden:
	cancel event if event-damagecause is void

on damage of player:
	set metadata tag "lastAttack" of attacker to now if attacker is a warden

	event-damagecause is void
	victim's helmet is a eye of ender
	last spawned warden is alive

	set victim's helmet to air
	teleport victim to location(0.5, 250, 0.5, world("world_the_end"))
	apply slow falling to victim for 25 seconds
	apply darkness to victim for 15 seconds

	play sound "minecraft:entity.warden.heartbeat" to victim
	play sound "minecraft:entity.ender_eye.death" to victim
	play sound "minecraft:music.end" to victim

on spawn of warden:
	cancel event if spawn reason isn't customized
		
		
