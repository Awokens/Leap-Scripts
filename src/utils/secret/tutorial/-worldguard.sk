options:
	posA: location(-24, -63, -24, world("world_tutorial"))
	posB: location(24, -55, 24, world("world_tutorial"))
	tutorial_spawn: location(21.5, -61.85, -20.5, world("world_tutorial"), 45, 0)

on break:
	world is "world_tutorial"
	cancel event if player's gamemode isn't creative
	
on place:
	world is "world_tutorial"
	cancel event if player's gamemode isn't creative

on drop:
	cancel event if world is "world_tutorial"

on pickup:
	cancel event if world is "world_tutorial"

on player world change:
	set metadata tag "changing" of player to true
	if player's world is "world_tutorial":
		set player's gamemode to adventure
		teleport player to {@tutorial_spawn}
	else:
		set player's gamemode to survival

command /tutorial:
	cooldown: 10 seconds
	cooldown message: &cPlease wait a moment before executing this command
	trigger:
		wait 5 ticks # incase you move right when you execute
		set {_init} to player's location
		
		loop 5 times:
			if distance between {_init} and player's location >= 1:
				send action bar "&cYou moved." to player
				exit
			send action bar "&aTeleporting to Tutorial in &f%6 - loop-value%" to player
			wait 1 second
		teleport player to {@tutorial_spawn}
		set player's gamemode to adventure
		send "", "A work in progress replacement of &a/bookguide", "To go back to the overworld, do &a/back", ""

command /back:
	trigger:
		if player's world isn't "world_tutorial":
			send "&cYou are not in the tutorial world"
			exit
		if player's bed is set:
			teleport player to player's bed
		else:
			teleport player to location(0.5, -63, 0.5, world("world"), 90, 0)
on load:
	delete bound with id "tutorial"
	create new bound with id "tutorial" between {@posA} and {@posB}

on damage:
	cancel event if world is "world_tutorial"
	
on bound exit with id "tutorial":
	wait 1 ticks
	if metadata tag "changing" of player is true:
		clear metadata tag "changing" of player
		exit

	world is "world_tutorial"
	teleport player to {@tutorial_spawn}
	
	