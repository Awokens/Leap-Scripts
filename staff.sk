options:
	server: "1105165795169947810"
	guild: "1105153184575541331"
	

on disconnect:
	reveal player to players

on join:

	set join message to "&a%player% has joined the server"
	set glowing of player to false
	if player has permission "staff.vanish":
		exit
	hide (players where [input's gamemode is spectator]) from player

	make embed:
		set title of embed to "%player% has joined the server"
		set embed color of the embed to hex "3ddf34"
	post last embed to channel with id {@server}

on quit:
	if player's gamemode is spectator:
		set quit message to ""
		exit
	set quit message to "&c%player% has left the server"
	make embed:
		set title of embed to "%player% has left the server"
		set embed color of the embed to hex "f75151"
	post (last embed) to channel with id {@server}

function unvanish(p: player):
	#if difference between last login of {_p} and now <= 5 seconds:
		#exit
	reveal {_p} to players
	set glowing of {_p} to false
	#exit
	#send "&a%{_p}% has joined the server" to players

	#make embed:
		#set title of embed to "%{_p}% has joined the server"
		#set embed color of the embed to hex "3ddf34"
	#post last embed to channel with id {@server}

on gamemode change to spectator:


	#send "&c%player% has left the server" to players
	set glowing of player to true
	#make embed:
	#	set title of embed to "%player% has left the server"
	#	set embed color of the embed to hex "f75151"
	#post (last embed) to channel with id {@server}

	loop players:
		if loop-player has permission "staff.vanish":
			reveal player to loop-player
		else:
			hide player from loop-player
	
	set {_i} to 0
	set {_id} to random uuid
	set metadata tag "session" of player to {_id}
	while player's gamemode is spectator:
		if metadata tag "session" of player isn't {_id}:
			exit
		send action bar "&a%{_i}%" to player
		add 1 to {_i}
		wait 1 second
on gamemode change to survival:
	unvanish(player)
	
on gamemode change to adventure:
	unvanish(player)
	
on gamemode change to creative:
	unvanish(player)
	
command /staffchat:
	aliases: /sc
	trigger:
		set {_state} to metadata tag "staffchat" of player
		if {_state} ? false is true:
			set {_state} to false
		else: 
			set {_state} to true
		set metadata tag "staffchat" of player to {_state}
		if {_state} is true:
			send "&fToggled staffchat to &aon"
		else:
			send "&fToggled staffchat to &coff"
	
command /world <world>:
	permission: *
	trigger:
		teleport player to spawn point of arg-1
		send "Teleported to world &a%arg-1%"
	
command /enderchest [<player>]:
	permission: *
	trigger:
		open (arg-1 ? player)'s enderchest to player
		
command /inventory <player>:
	permission: *
	trigger:
		open (arg-1)'s inventory to player

command /gmsp:
	permission: staff.gamemode.spectator
	trigger:
		set player's gamemode to spectator
		send "Set gamemode to %player's gamemode%"

command /gms:
	permission: staff.gamemode.survival
	trigger:
		set player's gamemode to survival
		send "Set gamemode to %player's gamemode%"
		
command /teleport <player> [<player>]:
	aliases: /tp
	permission: staff.teleport
	usage: /tp <player> [<player>]
	trigger:
		
		if arg-2 is set:
			teleport arg-1 to arg-2
			send "Teleport %arg-1% to %arg-2%"
		else:
			teleport player to arg-1
			send "Teleport you to %arg-1%"
		
		
command /kit:
	permission: *
	trigger:
		set {_first} to random integer from 2 to 8
		set {_second} to random integer from 4 to 16
		set {_third} to random integer from 4 to 16
		set {_fourth} to random integer from 4 to 16
	
		set {_nbt} to nbt from "{Items:[{Count:%{_first}%b,id:""minecraft:nautilus_shell""},{Count:%{_second}%b,id:""minecraft:oak_planks""},{Count:%{_third}%b,id:""minecraft:cobblestone""},{Count:%{_fourth}%b,id:""minecraft:dirt""}]}"
	
		set {_bundle} to bundle with nbt {_nbt}
	
		set {_rod} to fishing rod
		set {_max} to max durability of {_rod}
		set durability of {_rod} to random integer from ({_max} / 2) to 0
	
		set {_pickaxe} to stone pickaxe
		set {_max} to max durability of {_pickaxe}
		set durability of {_pickaxe} to random integer from ({_max} / 2) to 0
	
		set {_axe} to stone axe
		set {_max} to max durability of {_axe}
		set durability of {_axe} to random integer from ({_max} / 2) to 0
	
		give player {_rod}, {_bundle}, {_pickaxe} and {_axe}



command /createworld <string>:
	permission: *
	cooldown: 5 seconds
	trigger:
		set {_w} to world creator named arg-1
		set world type of {_w} to flat
		set generator settings of {_w} to "{""biome"": ""minecraft:plains"",""layers"": []}}"
		set should generate structures of {_w} to false
		load world from {_w}
		send "Created world %arg-1%"
		
		
	