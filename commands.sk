options:
	cooldown: 5 seconds
	cooldown_msg: &cPlease wait a few seconds before executing this command
	
function minehutAd() :: text component:
	set {_a} to text component of "%nl%Looking to advertise the server? &aClick this message%nl%"
	set hover event of {_a} to hover event showing "Make sure to paste the suggested command in the Minehut lobby, thank you."
	set click event of {_a} to click event to suggest command "/ad leap 1.19.X Void Survival For The Best Players"
	return {_a}
	
command /commands [<integer>]:
	usage: &c/commands <page number>
	trigger:
		
		send "", "&e&lCommands&r&e:"
		loop script commands:
			if {_commands::*} contains loop-value:
				continue
			if permission of command loop-value isn't "":
				continue
			add loop-value to {_commands::*}

		loop sorted {_commands::*}:
			send " - %loop-value%"
			
			
				

command /list:
	aliases: /players
	trigger:
		set {_p::*} to players where [input's gamemode isn't spectator]
		send "Player List &a(%size of {_p::*}%): ", and "&7 %{_p::*} ? "None"%" and ""
	

command /advertise:
	trigger:
		send component minehutAd()

command /fish:
	trigger:
		send "You have caught &b%fish caught stat of player%&f fish(es)"

command /playtime:
	trigger:
		send "Your play time is &a%time played of player%"

command /itemdrop:
	cooldown: 1 second
	cooldown message: {@cooldown_msg}
	trigger:
		set {_state} to {antiDrop::%player's uuid%}
		if {_state} ? false is true:
			set {_state} to false
		else: 
			set {_state} to true
		set {antiDrop::%player's uuid%} to {_state}
		if {_state} is true:
			send "&fToggled item drop to &aon"
		else:
			send "&fToggled item drop to &coff"


command /itempickup:
	cooldown: 1 second
	cooldown message: {@cooldown_msg}
	trigger:
		set {_state} to {antiPickUp::%player's uuid%}
		if {_state} ? false is true:
			set {_state} to false
		else:
			set {_state} to true
		set {antiPickUp::%player's uuid%} to {_state}
		if {_state} is true:
			send "&fToggled item pickup to &aon"
		else:
			send "&fToggled item pickup to &coff"

	
command /itemtoggle:
	trigger:
		set {_state} to {randomItems::%player's uuid%}
		if {_state} ? false is true:
			set {_state} to false
		else:
			set {_state} to true
		set {randomItems::%player's uuid%} to {_state}
		if {_state} is true:
			send "&fToggled items to &aon"
		else:
			send "&fToggled items to &coff"


command /donate:
	trigger:
		send "", "<link:https://www.paypal.com/paypalme/awokens>&fClick me to donate &b(PayPal)<reset>" and ""
		
command /discord:
	trigger:
		send "", "<link:https://discord.gg/DjcYsSqS7d>&fClick me to join the &9Discord<reset>" and ""

command /nonchest:
	aliases: /publicchest
	trigger:
		send "Opened the &auniversal&f player chest"
		if {nonchest} isn't set:
			set {nonchest} to chest inventory with 3 rows named "&8The Non Chest"
		open {nonchest} to player
		
command /suicide:
	cooldown: {@cooldown}
	cooldown message: {@cooldown_msg}
	trigger:
		if player's bed is set:
			send "You've committed yeet back to your &abed"
		else:
			send "You've committed yeet back to &aspawn"
		kill player

command /hat:
	cooldown: 1 second
	cooldown message: {@cooldown_msg}
	trigger:
		set {_item} to player's held item
		set held item to player's helmet
		set player's helmet to {_item}
		if player's helmet is air:
			send "Oh no... you exposed your &cugly face."
		else:
			send "You are wearing &a%type of player's helmet%&f as your helmet"